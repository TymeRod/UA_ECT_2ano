	.data
str:	.asciiz "ITED - orievA ed edadisrevinU"
	
	.text
	.globl main
	
strrev:		addiu	$sp, $sp, -16
		sw	$ra, 0($sp)
		sw	$s0, 4($sp)
		sw	$s1, 8($sp)
		sw	$s2, 12($sp)
	
		move	$s0, $a0	#str
		move	$s1, $a0	#p1
		move	$s2, $a0	#p2
					#
while:		lb	$t0, 0($s2)	#
		beq	$t0, '\0', endW	#while(*p2 != '\0')
		addi	$s2, $s2, 1	#p2++
		j	while		#
					#
endW:		addi	$s2,$s2,-1	#p2--
					#
while1:		bge	$s1, $s2, endW1	#while(p1 < p2)
					#
		move	$a0, $s1	#
		move	$a1, $s2	#
		jal 	exchange	#exchange(p1, p2)
					#
		addi	$s1, $s1, 1	#p1++
		addi	$s2, $s2, -1	#p2--
					#
		j	while1		#
					#
endW1:		move	$v0, $s0	#return str
					#
		lw	$ra, 0($sp)	#
		lw	$s0, 4($sp)	#
		lw	$s1, 8($sp)	#
		lw	$s2, 12($sp)	#
		addiu	$sp, $sp, 16	#
					#
		jr 	$ra		#
					#
					#
					#
exchange:	lb	$t0, 0($a0)	# 
		lb	$t1, 0($a1)	#
					#
		sb	$t1, 0($a0)	#
		sb	$t0, 0($a1)	#
					#
		jr	$ra		#
					#
					#
main:		addiu	$sp,$sp,-4	#
		sw	$ra, 0($sp)	#
					#
		la	$a0, str	#
		jal	strrev		#
		
		move	$a0, $v0
		li	$v0, 4
		syscall
		
		lw	$ra, 0($sp)
		addiu	$sp,$sp,4
		
		jr 	$ra
		
		
		